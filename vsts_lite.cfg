// Vslot Text Sender Lite by Salatiel (v 27/04/2025)
// This version does not include the vshadereditor.

vsts_attribute_get = [arg2 = (concatword $arg2 ":"); arg2 = (substr $arg1 (+ (strstr $arg1 $arg2) (strlen $arg2))); substr $arg2 0 (strstr $arg2 "|")]
vsts_attribute_edit = [
	strreplace $arg1 (concatword $arg2 ":" (vsts_attribute_get $arg1 $arg2) "|") (concatword $arg2 ":" $arg3 "|")
]

vsts_string_to_code = [
	loopconcatword s (strlen $arg1) [
		local n
		n = (strcode $arg1 $s)
		format "%1%2" $n (loopconcatword p (- 3 (strlen $n)) [result "!"])
	]
]

vsts_code_to_string = [
	loopconcatword c (strlen $arg1) [ codestr (substr $arg1 (* $c 3) 3) ]
]

vsts_write = [
	local coded i d coded_unique param_length
	coded = (vsts_string_to_code $arg1)
	i = 0
	d = 0
	param_length = (? (=s $arg3 "") 250 $arg3)

	if (!= $arg2 1) [
		while [<= $d (strlen $coded)] [
			if (> (rnd 100) 50) [
				appendword coded_unique "-"
			] [
				appendword coded_unique (substr $coded $d 1)
				d = (+ $d 1)
			]
		]
		coded = $coded_unique
	]

	vdelta [
		while [<= $i (strlen $coded)] [
			vshaderparam (format "%1%2%3" (? (= $i 0) "[" "") (substr $coded $i $param_length) (? (>= (+ $i $param_length) (strlen $coded)) "]" ""))
			i = (+ $i $param_length)
		]
	]
	compactvslots
]

vsts_join = [
	strreplace (strreplace (getvshaderparamnames $arg1) "-" "") " " ""
]

vsts_get_by_slot = [
	local params
	params = (vsts_join $arg1)
	result $params
]

vsts_get_by_match = [
	compactvslots
	local code_match search_slot all_slots match_start match_end
	code_match = (vsts_string_to_code $arg1)
	search_slot = (numslots)
	match_start = $arg2
	match_end = $arg3

	all_slots = []

	while [<= $search_slot (numvslots)] [
		append all_slots (looplistconcat s (vsts_get_by_slot $search_slot) [result (format "[%1]" $s)])
		search_slot = (+ $search_slot 1)
	]
	
	? (!=s $code_match "") (listfilter m $all_slots [!= (strstr (? (=s $match_start "") $m (substr $m $match_start $match_end)) $code_match) -1]) $all_slots
]

vsts_read = [
	local by_slot by_match final_result
	by_slot = (vsts_get_by_slot $arg1)
	by_match =  (vsts_get_by_match $arg1 $arg2 $arg3)
	final_result = (looplistconcat str (concat (listfilter bs $by_slot [= (strstr $by_match $bs) -1]) $by_match) [format "[%1]" (vsts_code_to_string $str)])
	result $final_result
]

vsts_sendfile = [
	if (findfile $arg1) [
		guieditor vsts_file -10 5
		textfocus vsts_file
		textload $arg1
		if (!=s $textshow "") [
				local filelen filesize
				filelen = (strlen $textshow)
				filesize = (? (< $filelen 1024) (format "%1 bytes" $filelen) (? (< $filelen 1048576) (format "%1 KB" (divf $filelen 1024)) (format "%1 MB" (divf $filelen 1048576))))
				
				vsts_write (format "name:%1|body:%2|size:%3|" $arg1 (vsts_string_to_code (format "%1" $textshow)) (vsts_string_to_code $filesize))
				
				if (vsts_read $arg1) [
					_vsts_echo (format "Wrote file ^"%1^" (%2) with %3 characters" $arg1 $filesize (strlen $textshow))
				] [
					_vsts_echo (format "^f2Could not write file ^"^f7%1^f2^", try again" $arg1)
				]
		] [
			_vsts_echo (format "^f2File ^"^f7%1^f2^" is empty" $arg1)
		]
	] [
		_vsts_echo (format "^f2File ^"^f7%1^f2^" not found" $arg1)
	]
]

vsts_getfile = [
	local file filename savename
	filename = $arg1
	savename = (? (=s $arg2 "") $arg1 $arg2)

	file = (vsts_read $arg1)

	if (=s $file "") [
		_vsts_echo (format "^f2File ^"%1^" not found" $arg1)
	] [
		local body_code
		body_code = (vsts_attribute_get $file "body")
	
		if (!=s $body_code "") [
			local filesize
			filesize = (vsts_code_to_string (vsts_attribute_get $file "size"))
	
			_vsts_echo (format "File ^"%1^" (%2) loaded with %3 characters" $arg1 $filesize (- (div (strlen $body_code) 3) 2))

			newgui _ [
				local content
				content = (vsts_code_to_string @body_code)
				guifield content 1
				textfocus content
				textsave (format "vsts/%1" @savename)
				newgui _ [
					if (findfile (format "vsts/%1" @@savename)) [
						_vsts_echo (format "File ^"vsts/%1^" ^f0saved successfully" @@@savename)
					] [
						_vsts_echo (format "File ^"vsts/%1^" ^f2could not be saved" @@@savename)
					]
					newgui _ []
				]
				cleargui
			]
			showgui _
		] [
			_vsts_echo (format "^f2Could not load ^f7%1^f2, not a valid file (missing 'body')" @filename)
		]
	]
]

_vsts_echo = [
	echo (format "^f7[VSTS-0.1^f~] %1" $arg1)
]

_vsts_echo "Vslot Text Sender Lite loaded"

nompedit 0 // some vcommands are disabled in multiplayer by default
